apply plugin: 'com.android.application'

def keystoreFile = rootProject.file("app/sp.jks")

android {
    if (keystoreFile.exists()) {
        signingConfigs {
            release {
                storeFile file("sp.jks")
                storePassword "smartpack"
                keyAlias "sp_key1"
                keyPassword "smartpack"
            }
        }
    }

    namespace 'com.sunilpaulmathew.snotz'
    compileSdk 34

    defaultConfig {
        applicationId "com.sunilpaulmathew.snotz"
        targetSdk 34
        versionCode 31
        versionName "v0.31"
        multiDexEnabled = true
        vectorDrawables.useSupportLibrary = true
        testFunctionalTest = false
        testHandleProfiling = false
    }

    buildTypes {
        release {
            if (keystoreFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = true
    }

    flavorDimensions.add("release")
    productFlavors {
        play {
            minSdk 19
            dimension "release"
        }
        fdroid {
            minSdk 17
            dimension "release"
        }
    }
    lint {
        abortOnError false
    }

}

dependencies {
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    playImplementation 'com.android.billingclient:billing:6.1.0'

    def sCommon_Version = 'aa1019da38'
    implementation "com.github.sunilpaulmathew.sCommon:adapters:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:credits:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:crashreporter:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:themeutils:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:translatorutils:${sCommon_Version}"

    implementation 'com.github.QuadFlask:colorpicker:0.0.15'
    implementation "com.google.android.material:material:1.11.0"
    implementation "com.google.code.gson:gson:2.10"
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
}